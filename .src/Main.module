' Gambas module file

Private server As New ServerSocket As "server"
Private port_number As Integer
Private socket_list As New Socket[]
Private $conf As String[]
Private webhooks As New Webhook[]
Private $triggerList As String[]
Private $log As Logger

Private $conFilter As String '' like Filter String
Private $conPort As Integer ''Port of the server
Private $conListen As Integer
Private $DNS As Boolean

Private Sub loadConst()
  
  Dim Conf As New Settings
  
  $conFilter = Conf["General/Filer", "*%*%*"]
  $conPort = Conf["Connection/Port", 9090]
  $conListen = Conf["Connection/Listen", 1]
  If $conPort = 9090 Then 
    Print("If you want to change the Port edit the Settings File: " & Conf.Path & "")
  Endif
  If $conListen = 1 Then 
    Print ("if you want to change the number of listening connections edit the Settings File: " & Conf.Path & "")
  Endif
  
  Conf["General/Filer"] = $conFilter
  Conf["Connection/Port"] = $conPort
  Conf["Connection/Listen"] = $conListen
  If Conf["PRG/BLOCK", 0] = 0 Then
    Conf["PRG/BLOCK"] = Application.Handle
  Else 
    $DNS = True
    
  Endif 
  
  conf.Save
  
End

Public Sub Print(msg As String, Optional Level As Integer = 3)
  
  $log.Log(msg, Level)
  Print msg
  
End

Public Sub loadWebhooks(all As Boolean)
  
  $triggerList = New String[]
  Print("Load Webhooks....")
  Print("---------Active-------------")
  Dim s As String
  Dim hook As Webhook
  
  For Each s In Dir(User.Home &/ ".webhook/")
    
    hook = New Webhook(s, False, True)
    If hook.Active Or all Then
      
      webhooks.Add(hook)
      $triggerList.Add("/" & hook.Name)
      If hook.Active Then
        Print("[Enable] Loaded " & hook.Name)
      Else 
        Print("[Disable] Loaded " & hook.Name)
      Endif
    Endif
  Next
  Print("-----------------------------")
  Print("Webhooks Ok...")
  
End

Public Sub Main() ''Main Code
  
  Dim start_Server As Boolean
  Dim get_New_ID As Boolean
  Dim set_new_trigger As Boolean
  Dim wtest As Webhook
  Dim s As String
  Dim hook As Webhook
  Dim toggle_webhook As Boolean
  Dim setup_programm As Boolean
  Dim install_To_systemd As Boolean
  Dim prg_exit As Boolean
  
  Args.Begin(Application.Name & " [options]")
  start_Server = Args.Has("s", "start", "Start Server")
  get_New_ID = Args.Has("i", "newid", "get a new Request ID for a new Client")
  set_new_trigger = Args.Has("w", "webhook", "new/edit Webhook Dialog")
  toggle_webhook = Args.has("t", "toggle", "Toggle a webhook Enable/Disable")
  setup_programm = Args.Has(Null, "setup", "Setup Dialog for the programm Server")
  install_To_systemd = Args.Has("i", "install", "Install Systemd Unit and Enable/Start")
  prg_exit = Args.Has(Null, "exit", "Save closing the Running Program")
  
  Args.End()
  
  If prg_exit Then
    prgEXIT()
  Endif
  
  $log = New Logger(4, "$(time): $(message)", "/var/log/webhook/" & Day(Now) & Month(Now) & Year(Now) & ".log")
  $log.Begin()
  Print("archive old log Files to /var/log/webhook/archive/" & Year(Now) &/ Month(Now))
  ArciveLog()
  Print("Hello world")
  
  If Not Exist(User.Home & "/.webhook") Then
    Try Mkdir User.Home & "/.webhook"
  Endif
  If Not Exist("/var/log/webhook") Then
    Try Mkdir "/var/log/webhook" 
    If Error Then 
      Print Error.Code
      Print Error.Text
      Print Error.Where
      Quit Error.code
    Else 
      Chgrp "/var/log/webhook" To "webhooklog"
      Chmod "/var/log/webhook" To "rwsrws---"
    Endif
  Endif
  
  If install_To_systemd Then 
    Shell "systemctl enable webhook" Wait
    Shell "Systemctl start webhook" Wait
  Endif
  
  If setup_programm Then
    Print "Setup Dialog"
    Setup.Main()
    Quit
  Endif
  
  If set_new_trigger Then
    loadWebhooks(True)
    Print("Add New/Edit Webhook...")
    
    AddTrigger.Add_Trigger(webhooks)
    Quit
  Endif
  
  If toggle_webhook Then 
    loadWebhooks(True)
    ToggleWebhook.ToggleWebhook(webhooks)
    Quit
  Endif
  
  ''Start of the Server
  If start_Server Then
    Print("load id_list")
    load_idlist()
    loadConst()
    Print("Load Config....")
    loadWebhooks(False)
    Print("Init Server....")
    port_number = $conPort
    init_server()
  Endif
  
  ''Get New ID 
  If get_New_ID Then 
    Print("load id_list")
    load_idlist()
    newid()
    Quit 
  Endif
  
End

Public Sub ArciveLog()
  
  Dim s As String
  Dim sY As Integer
  Dim sM As Integer
  Dim tar As New String[]
  
  For Each s In Dir("/var/log/webhook/")
    If s == "archive" Then 
    Else
      
      If s = Day(Now) & Month(Now) & Year(Now) & ".log" Then
      Else 
        sM = Month(Stat("/var/log/webhook/" & s).LastModified)
        SY = Year(Stat("/var/log/webhook/" & s).LastModified)
        If Not Exist("/var/log/webhook/archive") Then Mkdir "/var/log/webhook/archive" 
        If Not Exist("/var/log/webhook/archive" &/ sY) Then Mkdir "/var/log/webhook/archive" &/ sY
        If Not Exist("/var/log/webhook/archive" &/ sY &/ sM) Then Mkdir "/var/log/webhook/archive" &/ sY &/ sM
        Move "/var/log/webhook/" &/ s To "/var/log/webhook/archive" &/ sY &/ sM &/ s
        Print("/var/log/webhook/" &/ s & " Move To  /var/log/webhook/archive" &/ sY &/ sM &/ s)
        
      Endif
      If sM <> Month(Now) Or sY <> Year(Now) Then
        If tar.Find("/var/log/webhook/archive" &/ sY &/ sM) < 0 Then tar.Add("/var/log/webhook/archive" &/ sY &/ sM)
      Endif
    Endif
  Next
  For Each s In tar
    
    Print("Make tar of Path " & s)
    
    Exec ["tar", "-czf", s & ".tar.gz", s] Wait
    
  Next
  
End

Public Sub load_idlist()
  
  Dim split As String
  Dim conf As File
  
  If Not Exist(User.Home &/ ".webhook") Then Mkdir User.Home &/ ".webhook"
  If Exist("/usr/lib/webhook/client_id.conf") Then 
    conf = Open "/usr/lib/webhook/client_id.conf" For Read 
    split = Read #conf, Lof(conf)
    Close #conf
    $conf = Split(split, "\n", "", True)
    $conf.Delete(0) ''this is the Template of the file
  Else 
    $conf = ["none|none|none"]
  Endif
  
End

Public Sub newid()
  
  Dim s As String
  Dim id_array As New String[](3)''[Id,Name,description]
  Dim conf As File
  
  Print("Get New ID")
  s = "Name of the new Client: "
  Write #File.Out, s
  Flush #File.Out
  Line Input #File.in, id_array[1]
  Print "description of the client:"
  Line Input #File.in, id_array[2]
  Print("Gen id")
  genid(id_array)
  If Exist("/usr/lib/webhook/client_id.conf") Then
    While Exist_ID(["id=" & id_array[0]]) ''wenn true dann neu gen
      id_array[0] = ""
      genid(id_array)
    Wend
  Endif
  
  Print id_array[0]
  
  If Not Exist("/usr/lib/webhook") Then Mkdir "/usr/lib/webhook"
  If Exist("/usr/lib/webhook/client_id.conf") Then 
    conf = Open "/usr/lib/webhook/client_id.conf" For Read Write 
    Seek #conf, -1
  Else 
    conf = Open "/usr/lib/webhook/client_id.conf" For Read Write Create 
    s = "#    ID     |   Name   |   description  |\n"
    Write #conf, s
  Endif
  
  s = id_array[0] & "|" & id_array[1] & "|" & id_array[2] & gb.NewLine & gb.NewLine
  Write #conf, s
  Close #conf
  
End 

Public Sub Exist_ID(requested_args As String[]) As Boolean
  
  Dim s As String
  Dim id As String
  
  If requested_args.Find("id=*", gb.Like) >= 0 Then
    id = requested_args[requested_args.Find("id=*", gb.Like)]
    
    id = Right(id, id.Len - 3)
  Else 
    id = ""
  Endif 
  
  For Each s In $conf
    If id = Split(s, "|")[0] Then Return True
    
  Next
  Return False
  
End

Public Sub genid(id_array As String[])
  
  Dim i As Integer
  Dim tmp As Integer
  
  i = 30
  
  While i > 0
    i -= 1
    tmp = Rand(1, 3)
    Select tmp
      Case 1
        id_array[0] &= Chr(Rand(49, 57))
      Case 2
        id_array[0] &= Chr(Rand(65, 90))
      Case 3
        id_array[0] &= Chr(Rand(97, 122))
    End Select
  Wend
  
End

Public Sub init_server()''create the server
  
  If Not $DNS Then
    Signal[Signal.SIGUSR1].Catch
    Try server.Close()
    
    Print("Set Port to: " & port_number & " ...")
    server.Port = port_number
    server.Type = Net.Internet
    server.Listen($conListen)
    While Server.Status Not Net.Active
      Wait 0.1
      Print("Starting...")
    Wend
    Print("Server Started and Listen")
    
  Else 
    Error.Raise("DNS (Do Not Start) is Active Please look if the server not running (Lock file)")
  Endif 
  
End 

Public Sub server_Connection(RemoteHostIP As String)''Aufruf bei Verbindung
  
  Print(RemoteHostIP & " Conneted...")
  socket_list.Add(Server.Accept())
  
End

Public Sub Socket_Read()''Liest die Request vom Client
  
  Dim read_string As String
  Dim connection As Socket
  
  Print("Incomming Request\n----------------------------------")
  read_string = ""
  read_string = Read #Last, Lof(Last)
  Print(read_string)
  connection = Last
  Request(read_string, connection)
  Wait 1
  Print("Request done -> close connection")
  connection.Close()
  
End

Public Sub Request(Request As String, connection As Socket)''Wertet die Request aus und leitet sie weiter
  
  Dim request_lines As String[]
  Dim requested_path As String[]
  Dim requested_args As String[]
  Dim i As Integer
  
  request_lines = Split(Request, gb.NewLine)
  If request_lines.Find("GET*", gb.Like) >= 0 Then 
    requested_path = Split(request_lines[request_lines.Find("GET*", gb.Like)], " ")
    Print requested_path
    requested_args = Split(requested_path[1], "?&", "()", True, True)
    
    i = $triggerList.Find(requested_args[0])
    If i > -1 Then
      webhook_Action($triggerList[i], webhooks[i], requested_args, connection)
    Else 
      
      Send_ACK(connection, 400)
    Endif
    
  Endif
  
End

Public Sub webhook_Action(trigger As String, hook As Webhook, requested_args As String[], connection As Socket)
  
  Dim ID As Boolean
  Dim s As String
  Dim sa As String[]
  Dim Coll As New Collection
  Dim key As String
  Dim keyword As String
  Dim indexa As Integer
  Dim indexb As Integer
  Dim sb As String
  Dim sc As String
  Dim sd As String
  Dim c As Collection
  
  If hook.CheckID Then 
    ID = Exist_ID(requested_args)
    
  Else 
    ID = True
    Send_ACK(connection, 200)
  Endif
  
  If ID Then 
    Send_ACK(connection, 200)
    For Each key In hook.keyWordList
      If requested_args.Find(key & "=*", gb.Like) >= 0 Then
        keyword = requested_args[requested_args.Find(key & "=*", gb.Like)]
        Coll[key] = Right(keyword, keyword.Len - 1 - key.Len)
      Else 
        Coll[key] = ""
      Endif 
    Next
    
    For Each c In hook.CollAction
      s = c["Prg"] & c["Par"]
      
      sa = Split(s, " ", "\"")
      If sa.Find($conFilter, gb.Like) > -1 Then
        s = sa[sa.Find($conFilter, gb.like)]
        indexa = String.InStr(s, "%")
        indexb = String.InStr(s, "%", indexa + 1)
        sb = String.Left(s, indexa - 1)
        sc = String.Right(s, -indexb)
        sd = String.Mid(s, indexa + 1, indexb - indexa - 1)
        
        For Each key In hook.keyWordList
          If key == String.Left(sd, key.Len) Then
            
            sd = Coll[key]
            s = sb & sd & sc
            Print(s)
            sa[sa.Find($conFilter, gb.like)] = s
            Break 
          Endif
        Next
      Endif
      sa[1] = CHTTP(sa[1], CBool(c["ParHtmlConv"]))
      Exec sa Wait
      
    Next
  Else 
    Send_ACK(connection, 403)
  Endif
Catch 
  Print(Error.Code)
  Print(Error.Text)
  Print(Error.Where)
  
End

Public Sub CHTTP(uri As String, convert As Boolean) As String
  
  Dim nUri As String
  Dim i As Integer
  Dim s As String
  
  If convert Then
    nUri = ""
    i = 0
    While i <= String.Len(uri)
      i += 1
      s = String.Mid(uri, i, 1)
      Select s
        Case "&"
          nuri &= "%26"
        Case Else
          nUri &= s
      End Select
    Wend
    
    uri = nUri
  Endif
  
  Return uri
  
End

Public Sub Send_ACK(connection As Socket, success_code As Integer, Optional keyword As String)''Sendet die Antwort an den Client zurÃ¼ck
  
  Dim response As String
  
  Select success_code
    Case 200
      response = "HTTP/1.1 200 OK\nContent-Type: text/plain\nContent-Length: 2\n\nok"
      
    Case 403
      response = "HTTP/1.1 403 Forbidden\nContent-Type: text/plain\nConnection: close\nContent-Length: 24\n\nDie Anfrage ist verboten"
    Default 
      response = "HTTP/1.1 400 Bad Request\nContent-Type: text/plain\nConnection: close\nContent-Length: 38\n\nDie Anfrage enthaelt ungueltige Syntax."
      
  End Select 
  Write #connection, response, response.Len
  Flush #connection
  
End

Public Sub prgEXIT()
  
  Dim Conf As Settings
  Dim pid As Integer
  
  Conf = New Settings
  pid = Conf["PRG/BLOCK"]
  
  Signal.Send(pid, Signal.SIGUSR1)
  
  Print pid & " will be shutdown"
  Conf["PRG/BLOCK"] = 0
  Conf.Save()
  Quit 0
  
End

Public Sub Application_Signal(Signal As Integer)
  
  Print("SIGUSR1 get")
  Print("I shutdown!")
  Try server.Close()
  If Error Then 
    Print("Can't close the server")
  Else 
    Print("Server Closed")
  Endif
  
  Print("Goodbye!\n------------------")
  Quit
  
End
